#! /bin/csh -f
#
#  mir.cube      run some tests on a miriad data cube, using imsize= use it as a benchmark
#
#
set tmp=cube$$
mkdir $tmp
cd $tmp

set ftp=ftp://ftp.astro.umd.edu/pub/carma/data/
set cube=n6503co.tar.gz

set keep=1
set imsize=0

foreach a ($*)
  set $a
end

if ($imsize > 0) then
  # CUBE noise from scratch
  set in=cube$imsize
  imgen out=$in imsize=$imsize,$imsize,$imsize object=noise
else
  # CUBE from data repository
  set targz=$MIR/data/$cube

  if (-e $targz) then
    echo Using local copy $targz 
    tar zxf $targz
  else
    echo Grabbing remote copy $ftp/$cube
    wget $ftp/$cube
    if ($status) then
      echo No wget ... Trying curl $ftp/$cube
      curl $ftp/$cube | tar zxf -
    else
      tar zxf $cube
    endif
    if ($keep) cp $cube $MIR/data
  endif
  set in=$cube:r:r
endif

if (! -d $in) then
  echo Missing image cube dataset $in
  exit 1
endif

echo Running cubetest in=$in

echo $MIR/src/scripts/cubetest in=$in '$*' > runme
chmod +x runme
./runme >& cubetest.log
set err=$status

echo Logfile in $tmp/cubetest.log

