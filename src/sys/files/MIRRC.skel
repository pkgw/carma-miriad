#  -------------------------------------------------------------
#	Skeleton MIRRC file ... to be put in the local MIRIAD's
#	home directory.  It is essential that "standard" system
#	directories be in the user's search path (eg, to find
#	"awk").
# -------------------------------------------------------------
#  Environment variables to be locally specified:
#
#    $MIR ....... location of MIRIAD's home directory
#    $MIRHOST ... "sun3", "sun4", "sun4sol", "mips", or "convex"
#    $MIRXINC ... include directory that contains the X11 and Xm
#                 directories.  If you don't have the Motif libraries,
#                 then this is the directory that contains just the
#                 X11 directory.  If these reside in different
#                 directories or additional directories need to be
#                 specified, include them in a space separated list,
#                 eg, "/usr/openwin/include /usr/dt/include"
#                 would search for X11 and Xm include directories
#                 first /usr/openwin/include and then
#                 /usr/dt/include.
#    $MIRXLIB ... directory that contains the X11 libraries (set to 
#                 null string if -X11 is not installed).  Note that
#                 additional directories can be specified in a
#                 space separated list, eg,
#                 "/usr/local/lib/X11 /usr/tmp/lib/X11" will cause the
#                 linker to search first /usr/local/lib/X11 and then
#                 /usr/tmp/lib/X11 to resolve external references.
#  -------------------------------------------------------------
#  The example shown below is a skeleton file.
#  The install.miriad script will patch this file and place it in $MIR
#  -------------------------------------------------------------
if ($?MIR == 0) then
  setenv MIR      "/mir_rootdir"
endif
if (-e $MIR/build) then
  set auto=1
  echo Warning: using new build system
else
  set auto=0
endif
setenv MIRHOST  "mir_hosttype"
setenv MIRXINC  "/usr/openwin/include"
setenv MIRXLIB  "/usr/local/lib/X11"
setenv AIPSTV   "XASIN"
if ($auto) then
  setenv MIRBIN   $MIR/build/bin
  setenv MIRLIB   $MIR/build/lib
else
  setenv MIRBIN   $MIR/bin/$MIRHOST
  setenv MIRLIB   $MIR/lib/$MIRHOST
endif
setenv MIRCAT   $MIR/cat
setenv MIRDEF   .
setenv MIRDOC   $MIR/doc
setenv MIRINC   $MIR/src/inc

setenv MIRNEWS  $MIR/news
setenv MIRPAGER "doc"
setenv MIRPDOC  $MIR/doc/prog
setenv MIRPROG  $MIR/src/prog
setenv MIRSDOC  $MIR/doc/subs
setenv MIRSRC   $MIR/src
setenv MIRSUBS  $MIR/src/subs
setenv MIRTEL   MIRTEL:unknown

if ($auto) then
 source $MIRLIB/pgplot-miriad-remix/pgplot-config.csh
else
  setenv PGPLOT_DIR  $MIRLIB
endif

if (`echo ":${PATH}:" | grep -c ":\.:"` > 0) then
   set path = (`echo ":${PATH}:"|sed "s&:.:&:.:${MIRBIN}:&"|sed 's/:/ /g'`)
else
   set path = ($MIRBIN $path)
endif

if (-d $MIR/opt/bin) set path=($MIR/opt/bin $path)

alias   mirfind         mir.find
alias   mirhelp         mir.help
alias   mirindex        mir.index
alias   mirbug          mir.bug.csh bug
alias   mirfeedback     mir.bug.csh feedback
#  -------------------------------------------------------------
#  Karma, if present
if (-e $MIR/borrow/karma/.login) then
  echo Loading karma tools
  setenv KARMABASE $MIR/borrow/karma/
  source $KARMABASE/.login
  if ($MIRHOST == "darwin") then
    if ($?DYLD_LIBRARY_PATH) then
      setenv DYLD_LIBRARY_PATH $KARMABASE/lib:$DYLD_LIBRARY_PATH
    else
      setenv DYLD_LIBRARY_PATH $KARMABASE/lib
    endif
  else
    if ($?LD_LIBRARY_PATH) then
      setenv LD_LIBRARY_PATH $KARMABASE/lib:$LD_LIBRARY_PATH
    else
      setenv LD_LIBRARY_PATH $KARMABASE/lib
    endif
  endif
endif

#  NEMO, if present
if (-e   $MIR/borrow/nemo/nemo_start.csh) then
  source $MIR/borrow/nemo/nemo_start.csh
endif

#  MIS, if present
if (-e   $MIR/borrow/mis/mis_start.csh) then
  source $MIR/borrow/mis/mis_start.csh
endif

#  EGN, if present
if (-e   $MIR/borrow/egn/egn_start.csh) then
  source $MIR/borrow/egn/egn_start.csh
endif

#  warning: if EGN and MIS both exist, there's trouble.

#  CASA(CORE), if present - this is where carmafiller likely lives (2013)
if (-e   $MIR/opt/casa) then
  set path = ($MIR/opt/casa/bin $path)
  if ($?LD_LIBRARY_PATH) then
    setenv LD_LIBRARY_PATH $MIR/opt/casa/lib:$LD_LIBRARY_PATH
  else
    setenv LD_LIBRARY_PATH $KARMABASE/lib
  endif
endif


#
#  If the file $MIR/MIRRC.local exists, execute it now.
#
if (-e $MIR/MIRRC.local) then
  source $MIR/MIRRC.local
endif
# 
#  If the user's file $HOME/.mirrc exists, execute it now.
#
if ($?HOME) then
  if (-e $HOME/.mirrc) then
    source $HOME/.mirrc
  endif
endif

#  -------------------------------------------------------------
